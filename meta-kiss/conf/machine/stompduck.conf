# Machine configuration for the Stomp Duck product.
#
# This is a fictitious product. These settings are actually for the
# STM32MP157A-DK1.

require include/common.inc

# Adds "stm32mp1" to MACHINEOVERRIDES, and ultimately to OVERRIDES
require conf/machine/include/soc-family.inc
SOC_FAMILY = "stm32mp1"

# Defines instruction sets and tunes for the CPU in the SoC
require conf/machine/include/arm/armv7a/tune-cortexa7.inc
DEFAULTTUNE = "cortexa7thf-neon-vfpv4"

# Basic info about the hardware
MACHINE_FEATURES = "usbhost usbgadget alsa screen pci tpm2"
SERIAL_CONSOLES = "115200;ttySTM0"

# Select kernel recipe, set parameters for kernel build
PREFERRED_PROVIDER_virtual/kernel ?= "linux-kiss"
KERNEL_DEVICETREE = "st/stm32mp157a-dk1.dtb"
KERNEL_IMAGETYPE = "zImage"

# Select bootloader recipe, set parameters for bootloader build
PREFERRED_PROVIDER_virtual/bootloader ?= "u-boot-kiss"
UBOOT_ARCH = "arm"
UBOOT_MACHINE = "stm32mp15_defconfig"
UBOOT_ENTRYPOINT = "0xC0800000"

# Build trusted-firmware-a for STM32MP1
UBOOT_BINARY = "u-boot-nodtb.${UBOOT_SUFFIX}"
UBOOT_MAKE_TARGET:append = " DEVICE_TREE=stm32mp157a-dk1"
TFA_PLATFORM = "stm32mp1"
TFA_BUILD_TARGET = "fip all"
TFA_INSTALL_TARGET = "fip tf-a-stm32mp157a-dk1.stm32"
EXTRA_OEMAKE:append:pn-trusted-firmware-a = " ARCH=aarch32 ARM_ARCH_MAJOR=7"
EXTRA_OEMAKE:append:pn-trusted-firmware-a = " AARCH32_SP=sp_min"
EXTRA_OEMAKE:append:pn-trusted-firmware-a = " DTB_FILE_NAME=stm32mp157a-dk1.dtb"
EXTRA_OEMAKE:append:pn-trusted-firmware-a = " BL33_CFG=${DEPLOY_DIR_IMAGE}/u-boot.dtb"
EXTRA_OEMAKE:append:pn-trusted-firmware-a = " STM32MP_SDMMC=1"
EXTRA_OEMAKE:append:pn-trusted-firmware-a = " BL33=${DEPLOY_DIR_IMAGE}/u-boot-nodtb.bin"

# This machine has the rootfs on partition 5
UBOOT_EXTLINUX_ROOT = "root=/dev/mmcblk0p5"

# Needed to boot (wic will copy it into the flashable image)
EXTRA_IMAGEDEPENDS:append = " trusted-firmware-a"
